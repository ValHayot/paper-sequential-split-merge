set terminal png font ',12'
set border 3 front lt black linewidth 1.000 dashtype solid
set boxwidth 0.5 absolute
set style fill solid 0.50 border lt -1
unset key
set pointsize 0.5
set minussign
set style data boxplot
set xtics border in scale 0,0 nomirror norotate  autojustify
set xtics  norangelimit 

set xtics ("Slices" 1, "Blocks" 2) 
set xtics font ',10'
set ytics font ',10'
set ytics border out scale 1,0.5 nomirror norotate  autojustify
set ydata time
set timefmt "%Mm%Ss"
set format y "%Mm%Ss"
set ylabel "Total processing time"
set xlabel "Merging strategy"
set output "totalmergetimeSSD1.png"
plot "totaltime.dat" using (1):1, '' using (2):2

set output "totalmergetimeHDD1.png"
plot 'totaltime.dat' using (1):6, '' using (2):7

set xtics (6.52E8, 1.63E9, 9.78E9, 1.30E10) border out scale 1,0.5 rotate by 25 right 
set xlabel "Available memory (bytes)"
set logscale x
set boxwidth 0.1
#set autoscale x
set output "ClusterReadsSlicesSSD.png"
plot [5e8:2e10] 'totaltime.dat' using (6.52E8): 1 ,'' using (1.63E9): 3, '' using (9.78E9): 4, '' using (1.30E10): 5

set xtics ("SRead" 1, "SWrite" 2, "BRead" 3, "BSeek" 4, "BWrite" 5) 
unset format y 
unset ydata
unset xrange
unset logscale x
unset format x
set boxwidth 0.5
set xlabel "Merging strategy"
set ylabel "Time (s)"
set output "ssdBreakDown1.png"
plot 'totaltime.dat' using (1):8, '' using (2):9, '' using (3):12, '' using (4):13, '' using (5):14
set output "hddBreakDown1.png"
plot 'totaltime.dat' using (1):10, '' using (2):11, '' using (3):15, '' using (4):16, '' using (5):17
