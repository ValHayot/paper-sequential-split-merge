#!/usr/bin/gnuplot

set terminal pdf font ',16'
set border 3 front lt black linewidth 1.000 dashtype solid
set boxwidth 0.5 absolute
set style fill solid 0.50 border lt -1
unset key
set pointsize 0.5
set minussign
set style data boxplot
set xtics border in scale 0,0 nomirror norotate  autojustify
set xtics  norangelimit 

filename='./data/blocks-slices/totaltime.dat'
outputdir='./figures/blocks-slices/'

####################
# Slices vs blocks #
####################

set xtics ("Blocks" 1, "Slices" 2) 
#set xtics font ',10'
#set ytics font ',10'
set ytics border out scale 1,0.5 nomirror norotate  autojustify
set ylabel "Total merging time (s)"

# Total time

 ## SSD
set output outputdir."totalmergetimeSSD1.pdf"
plot [][0:3600] filename using (1):2, '' using (2):1

 ## HDD
set output outputdir."totalmergetimeHDD1.pdf"
plot [][0:3600] filename using (1):7, '' using (2):6

# Breakdown

set xtics ("SRead" 4, "SWrite" 5, "BRead" 1, "BSeek" 2, "BWrite" 3) 
unset format y 
unset ydata
unset xrange
unset logscale x
unset format x
set boxwidth 0.5
set ylabel "Time (s)"

 ## SSD
set output outputdir."ssdBreakDown1.pdf"
plot [][0:2500] filename using (4):8,\
     '' using (5):9,\
     '' using (1):12,\
     '' using (2):13,\
     '' using (3):14

 # HDD
set output outputdir."hddBreakDown1.pdf"
plot [][0:2500] filename using (4):10,\
     '' using (5):11,\
     '' using (1):15,\
     '' using (2):16,\
     '' using (3):17

#################
# Cluster reads #
#################

# set xtics (6.52E8, 1.63E9, 9.78E9, 1.30E10) border out scale 1,0.5 rotate by 25 right 
# set xlabel "Available memory (bytes)"
# set logscale x
# set boxwidth 0.1
# #set autoscale x
# set output "ClusterReadsSlicesSSD.pdf"
# plot [5e8:2e10] filename using (6.52E8): 1 ,\
#      '' using (1.63E9): 3,\
#      '' using (9.78E9): 4,\
#      '' using (1.30E10): 5


