#!/usr/bin/env gnuplot

set terminal pdf font ',16'
set xlabel "Memory (GB)"
set ylabel "Number of seeks"

### Cluster reads 

# case 1: merge only incomplete rows of blocks
m(x)=(R*b)/n*floor(x*n/(R*b))
f(x)=n+ceil((R*b)/(nu**2*m(x)))*D**2

# case 2: merge complete rows of blocks
n(x)=R*b/(nu**2)*floor(x*nu**2/(R*b))
g(x)=n+ceil((R*b)/(nu*n(x)))*D

# case 3: merge complete slices of blocks
o(x)=R*b/nu*floor(x*nu/(R*b))
h(x)=n+ceil(R*b/o(x))

creads(x) = x < m1 ? f(x) : \
            x < m2 ? g(x) : \
	    h(x)

### Multiple reads

# case 1: merge voxels
p(x)=floor(x/(d*b))*d*b
a(x)=ceil((R*b)/p(x))*(p(x)*nu/(D*b)+1)-p(x)*nu/(D*b)+(nu*D**2 % floor(x/(d*b)))

# case 2: merge rows
q(x)=floor(x/(D*b))*D*b
b(x)=ceil((R*b)/q(x))*(nu+1)

# case 3: merge rows of tiles
r(x)=floor(x/(d*D*b))*D*b*d
#c(x)=ceil((R*b)/r(x))*(r(x)*nu**2/(D**2*b)+1)
c(x)=ceil((R*b)/r(x))*(r(x)*nu**2/(D**2*b)+1)-r(x)*nu**2/(D**2*b)+nu*(nu*D %floor(x/(d*D*b)))

# case 4: merge slices of tiles
s(x)=floor(x/(D**2*b))*D**2*b
d(x)=ceil((R*b)/s(x))*(nu**2+1)

# case 5: merge slices of blocks
u(x)=floor(x/((D**2)*d*b))*(D**2*d*b)
#e(x)=ceil((R*b)/u(x))*(u(x)*n/(R*b)+1)
e(x)=floor((R*b)/u(x))*(u(x)*n/(R*b)+1)-u(x)*n/(R*b)+nu**2*(n/(nu**2)%floor(x/((D**2)*d*b)))

mreads(x) =    x < m3 ? a(x):\
	       x < m4 ? b(x):\
	       x < m5 ? c(x):\
               x < m6 ? d(x):\
	       e(x) 

## BigBrain
D=3458
R=D**3
b=2
nu=5
n=nu**3
d=D/nu

# Interval bounds
 
## Cluster reads
m1=(R*b)/nu**2
m2=(R*b)/nu
 ## Multiple reads
m3=D*b
m4=D**2/nu*b
m5=D**2*b
m6=D**3/nu*b

## Cluster reads only
ymax=8000
set xtics ("10" 10*1024**3, "20" 20*1024**3, "30" 30*1024**3,\
           "40" 40*1024**3, "50" 50*1024**3)	
set output "./figures/model-big-brain-creads.pdf"
set obj 1 rect from 0, 1 to m1,ymax fc rgb "#AAAAAAAA"
set obj 2 rect from m1, 1 to m2,ymax fc rgb "#FFFFFFFF"
set label 1 at m1, 0.9*ymax " Case 2"    
set obj 3 rect from m2, 1 to b*R,ymax fc rgb "#AAAAAAAA"
set label 2 at m2, 0.9*ymax " Case 3"
plot [0:b*R][0:ymax] creads(x)  lw 3 lt 1 title "Cluster reads"
unset logscale y
unset obj 1
unset obj 2
unset obj 3
unset label 1
unset label 2

## Multiple reads only
set output "./figures/model-big-brain-mreads.pdf" 
#ymax=3000
set obj 1 rect from 0, 1 to m3,ymax fc rgb "#AAAAAAAA"
set obj 2 rect from m3, 1 to m4,ymax fc rgb "#FFFFFFFF"   
set obj 3 rect from m4, 1 to m5,ymax fc rgb "#AAAAAAAA"
set obj 4 rect from m5, 1 to m6,ymax fc rgb "#FFFFFFFF"
set obj 5 rect from m6, 1 to b*R,ymax fc rgb "#AAAAAAAA"
set label 1 at m5, 0.9*ymax "   Case 4"
set label 2 at m6, 0.9*ymax "   Case 5"
plot [0:b*R][0:ymax] mreads(x)  lw 3 lt 2 title "Multiple reads"
unset logscale y
unset obj 1
unset obj 2
unset obj 3
unset obj 4
unset obj 5
unset label 1
unset label 2

## Comparison
set output "./figures/model-big-brain-comparison.pdf"

### min and max plotted memory values
min=m1	  
max=m2

# Plot
set xtics ("2" 2*1024**3, "4" 4*1024**3, "6" 6*1024**3,\
           "8" 8*1024**3, "10" 10*1024**3, "12" 12*1024**3,\
           "14" 14*1024**3, "16" 16*1024**3)
plot  [min:max][0:]            creads(x) lw 3 title "Cluster reads",\
			       mreads(x) lw 3 title "Multiple reads"			     

# Computes and prints values to be ploted later, by scripts/mreads/number_of_seeks.gnuplot
buffslices(x)=n+ceil((b*R)/x)

v0=n+n*d**2
v1=creads(3*1024**3)
v2=creads(6*1024**3)
v3=creads(9*1024**3)
v4=creads(12*1024**3)
v5=creads(16*1024**3)
v6=mreads(3*1024**3)
v7=mreads(6*1024**3)
v8=mreads(9*1024**3)
v9=mreads(12*1024**3)
v10=mreads(16*1024**3)
v11=2*n
v12=buffslices(3*1024**3)
v13=buffslices(6*1024**3)
v14=buffslices(9*1024**3)
v15=buffslices(12*1024**3)
v16=buffslices(16*1024**3)
#  1.naiveblocks 2.cr3g 3.cr6g 4.cr9g 5.cr12g 6.cr16g\
   7.mr3g 8.mr6g 9.mr9g 10.mr12g 11.mr16g 12.naiveslices\
   13. buffsl3g 14. buffsl6g 15. buffsl9g 16. buffsl12g 17. buffsl16g
print v0, v1, v2, v3, v4, v5, v6, v7, v8, v9, v10, v11, v12, v13, v14, v15, v16


### Comparison with more blocks
set output "./figures/model-big-brain-comparison-moreblocks.pdf" 
R=D**3
b=2
nu=40
n=nu**3
d=D/nu

# Interval bounds

## Cluster reads
m1=(R*b)/nu**2
m2=(R*b)/nu
 ## Multiple reads
m3=D*b
m4=D**2/nu*b
m5=D**2*b
m6=D**3/nu*b

# min and max plotted memory values
min=m1
max=m2

# Plot
set xtics ("0.1" 0.1*1024**3, "0.3" 0.3*1024**3, "0.5" 0.5*1024**3,\
           "0.7" 0.7*1024**3, "0.9" 0.9*1024**3, "1.1" 1.1*1024**3,\
           "1.3" 1.3*1024**3, "1.5" 1.5*1024**3, "1.7" 1.7*1024**3)	
set ytics ("1" 10**6, "2" 2*10**6, "3" 3*10**6, "4" 4*10**6, "5" 5*10**6)
set ylabel "Number of seeks (x10^6)
plot  [min:max][0:]            creads(x) lw 3 title "Cluster reads",\
			       mreads(x) lw 3 title "Multiple reads"			     
