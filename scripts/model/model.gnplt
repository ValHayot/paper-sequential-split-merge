#!/usr/bin/env gnuplot

set terminal pdf font ',16'
set xlabel "Memory (GB)"
set ylabel "Number of seeks"

### Cluster reads 

# case 1: merge only incomplete rows of blocks
m(x)=(R*b)/n*floor(x*n/(R*b))
f(x)=n+ceil((R*b)/(nu**2*m(x)))*D**2

# case 2: merge complete rows of blocks
n(x)=R*b/(nu**2)*floor(x*nu**2/(R*b))
g(x)=n+ceil((R*b)/(nu*n(x)))*D

# case 3: merge complete slices of blocks
o(x)=R*b/nu*floor(x*nu/(R*b))
h(x)=n+ceil(R*b/o(x))

creads(x) = x < m1 ? f(x) : \
            x < m2 ? g(x) : \
	    h(x)

### Multiple reads

# case 1: merge voxels
vi1(x)=D*b
k1(x)=floor(x/vi1(x))
mprime1(x)=k1(x)*vi1(x)
a(x)=ceil((R*b)/mprime1(x))*(k1(x)+1)-k1(x)+(nu*D**2 % k1(x))

# case 2: merge rows
vi2(x)=D*b
k2(x)=floor(x/vi2(x))
mprime2(x)=k2(x)*vi2(x)
b(x)=ceil((R*b)/mprime2(x))*(nu+1)

# case 3: merge rows of tiles
vi3(x)=D*d*b
k3(x)=floor(x/vi3(x))
mprime3(x)=k3(x)*vi3(x)
c(x)=ceil((R*b)/mprime3(x))*(k3(x)*nu+1)-k3(x)*nu+nu*(nu*D % k3(x))

# case 4: merge slices of tiles
vi4(x)=D*D*b
k4(x)=floor(x/vi4(x))
mprime4(x)=k4(x)*vi4(x)
d(x)=ceil((R*b)/mprime4(x))*(nu**2+1)

# case 5: merge slices of blocks
vi5(x)=((D**2)*d*b)
k5(x)=floor(x/vi5(x))
mprime5(x)=k5(x)*vi5(x)
e(x)=ceil((R*b)/mprime5(x))*(k5(x)*nu**2+1)-k5(x)*nu**2+nu**2*(nu % k5(x))

mreads(x) =    x < m3 ? a(x):\
	       x < m4 ? b(x):\
	       x < m5 ? c(x):\
               x < m6 ? d(x):\
	       e(x) 

## BigBrain
D=3458
R=D**3
b=2
nu=5
n=nu**3
d=D/nu

# Interval bounds
 
## Cluster reads
m1=(R*b)/nu**2
m2=(R*b)/nu
 ## Multiple reads
m3=D*b
m4=D**2/nu*b
m5=D**2*b
m6=D**3/nu*b

## Comparison
set output "./figures/model-big-brain-comparison.pdf"

### min and max plotted memory values
min=m1
max=m2

ymax=18000
set obj 1 rect from 0, 1 to m1,ymax fc rgb "#AAAAAAAA"
set label 2 at 2.5*m1, 0.65*ymax "    Case 2"    
set obj 2 rect from m1, 1 to m2,ymax fc rgb "#FFFFFFFF"

# Plot
set xtics ("2" 2*1024**3, "4" 4*1024**3, "6" 6*1024**3,\
           "8" 8*1024**3, "10" 10*1024**3, "12" 12*1024**3,\
           "14" 14*1024**3, "16" 16*1024**3)
#set ytics ("1" 10**7, "2" 2*10**7, "3" 3*10**7, "4" 4*10**7, "5" 5*10**7)

plot  [min:max][0:]            creads(x) lw 3 title "Cluster reads",\
			       mreads(x) lw 3 title "Multiple reads"			     

# Computes and prints values to be ploted later, by scripts/mreads/number_of_seeks.gnuplot
buffslices(x)=n+ceil((b*R)/x)

v0=n+n*d**2
v1=creads(3*1024**3)
v2=creads(6*1024**3)
v3=creads(9*1024**3)
v4=creads(12*1024**3)
v5=creads(16*1024**3)
v6=mreads(3*1024**3)
v7=mreads(6*1024**3)
v8=mreads(9*1024**3)
v9=mreads(12*1024**3)
v10=mreads(16*1024**3)
v11=2*n
v12=buffslices(3*1024**3)
v13=buffslices(6*1024**3)
v14=buffslices(9*1024**3)
v15=buffslices(12*1024**3)
v16=buffslices(16*1024**3)
#  1.naiveblocks 2.cr3g 3.cr6g 4.cr9g 5.cr12g 6.cr16g\
   7.mr3g 8.mr6g 9.mr9g 10.mr12g 11.mr16g 12.naiveslices\
   13. buffsl3g 14. buffsl6g 15. buffsl9g 16. buffsl12g 17. buffsl16g
print v0, v1, v2, v3, v4, v5, v6, v7, v8, v9, v10, v11, v12, v13, v14, v15, v16


### Comparison with more blocks
set output "./figures/model-big-brain-comparison-moreblocks.pdf"
D=3458
R=D**3
b=2
nu=100
n=nu**3
d=D/nu

# Interval bounds

## Cluster reads
m1=(R*b)/nu**2
m2=(R*b)/nu
 ## Multiple reads
m3=D*b
m4=D**2/nu*b
m5=D**2*b
m6=D**3/nu*b

# min and max plotted memory values
min=m1/4
max=m2/5
ymax=5*10**7
set obj 1 rect from 0, 1 to m1,ymax fc rgb "#AAAAAAAA"
set label 2 at 8*m1, 0.65*ymax "    Case 2"    
set obj 2 rect from m1, 1 to m2,ymax fc rgb "#FFFFFFFF"
set label 1 at m1, 0.65*ymax "    Case 1"
ym=0.65*ymax
a=-5*1024**2
set arrow from m1-a,ym rto a*1.3,0

set xlabel "Memory (MB)"
# Plot
set xtics ("20" 20*1024**2,\
           "40" 40*1024**2,\
	   "60" 60*1024**2,\
           "80" 80*1024**2,\
	   "100" 100*1024**2,\
	   "120" 120*1024**2,\
	   "140" 140*1024**2)
set ytics ("1" 10**7, "2" 2*10**7, "3" 3*10**7, "4" 4*10**7, "5" 5*10**7)
set ylabel "Number of seeks (x10^7)
plot  [min:max][0:]        creads(x) lw 3 title "Cluster reads",\
			       mreads(x) lw 3 title "Multiple reads"			     
